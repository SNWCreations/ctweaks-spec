plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = 'snw.mods.ctweaks'
version = '0.1.0'

repositories {
    mavenCentral()
    maven {
        url = "https://jitpack.io"
    }
}

def lombok = "org.projectlombok:lombok:1.18.32"
def adventureVer = "4.14.0"
dependencies {
    api 'com.github.SNWCreations:libprotocol:master-SNAPSHOT'
    api "net.kyori:adventure-api:$adventureVer"
    compileOnly "net.kyori:adventure-text-serializer-gson:$adventureVer"


    compileOnly lombok
    annotationProcessor lombok

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        artifact(MavenPublication) {
            from components.java
        }
    }

    repositories {
        mavenLocal()
    }
}